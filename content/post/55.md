---
title: Meta Trader 5 のインディケーター (Gerald Appel の MACD) を作る
date: 2016-11-06T17:19:50+09:00
categories:
  - トレード
tags:
  - トレード
---

為替の取引をするのに Meta Trader 5(MT5) を使っているのですが、MACD を使おうと思った時に、このインディケーターの開発者である Gerald Appel の提唱するものと違うようなので、自分で作ってみました。

<!--more-->

## 違い

次の画像のインディケーターの上側が MT5 の標準の MACD です。
下側が Gerald Appel の提唱する MACD です。

![](/img/55-01.png)

上側のヒストグラムで描画されているものが MACD ラインです。
下側の青いラインで描画されているラインに対応します。

上側と下側の赤い点線で描画されているものがシグナルラインです。
シグナルラインは同じに描画されているように見えますが、上側が単純移動平均で、下側が指数平滑移動平均になっています。

MT5 の標準の MACD には Gerald Appel のいうところのヒストグラムがありません。
MACD ラインがヒストグラムで描画されているため少し混乱しますね。

## MACD?

MACD の計算方法と活用方法については検索すればたくさん出てきます。
次のリンクとかを参考にしています。

[FX 実戦チャート術 第2回 ボリンジャーバンド・MACD｜FX｜外為オンライン　FX取引 － あなたの為の、外為を。](http://www.gaitameonline.com/academy_chart02.jsp)

MACD を使いたかったのは、投資苑を読んで、ダイバージェンスとして活用したかったからです。

## ソース

MT5 の標準の MACD のソースを修正しました。

```c++
//+------------------------------------------------------------------+
//|                                                        MACD2.mq5 |
//|                        Copyright 2009, MetaQuotes Software Corp. |
//|                                              http://www.mql5.com |
//+------------------------------------------------------------------+
#property copyright   "2009, MetaQuotes Software Corp."
#property link        "http://www.mql5.com"
#property description "Moving Average Convergence/Divergence"
#include <MovingAverages.mqh>
//--- indicator settings
#property indicator_separate_window
#property indicator_buffers 5
#property indicator_plots   3
#property indicator_type1   DRAW_LINE
#property indicator_type2   DRAW_LINE
#property indicator_type3   DRAW_HISTOGRAM
#property indicator_color1  Blue
#property indicator_color2  Red
#property indicator_color3  Silver
#property indicator_width1  1
#property indicator_width2  1
#property indicator_width3  1
#property indicator_label1  "MACD"
#property indicator_label2  "Signal"
#property indicator_label3  "Histogram"
#property indicator_style1  STYLE_SOLID
#property indicator_style2  STYLE_DOT
#property indicator_style3  STYLE_SOLID
//--- input parameters
input int                InpFastEMA=12;               // Fast EMA period
input int                InpSlowEMA=26;               // Slow EMA period
input int                InpSignalEMA=9;              // Signal EMA period
input ENUM_APPLIED_PRICE InpAppliedPrice=PRICE_CLOSE; // Applied price
//--- indicator buffers
double                   ExtMacdBuffer[];
double                   ExtSignalBuffer[];
double                   ExtHistogramBuffer[];
double                   ExtFastMaBuffer[];
double                   ExtSlowMaBuffer[];
//--- MA handles
int                      ExtFastMaHandle;
int                      ExtSlowMaHandle;
//+------------------------------------------------------------------+
//| Custom indicator initialization function                         |
//+------------------------------------------------------------------+
void OnInit()
  {
//--- indicator buffers mapping
   SetIndexBuffer(0,ExtMacdBuffer,INDICATOR_DATA);
   SetIndexBuffer(1,ExtSignalBuffer,INDICATOR_DATA);
   SetIndexBuffer(2,ExtHistogramBuffer,INDICATOR_DATA);
   SetIndexBuffer(3,ExtFastMaBuffer,INDICATOR_CALCULATIONS);
   SetIndexBuffer(4,ExtSlowMaBuffer,INDICATOR_CALCULATIONS);
//--- sets first bar from what index will be drawn
   PlotIndexSetInteger(1,PLOT_DRAW_BEGIN,InpSignalEMA-1);
//--- name for Dindicator subwindow label
   IndicatorSetString(INDICATOR_SHORTNAME,"MACD("+string(InpFastEMA)+","+string(InpSlowEMA)+","+string(InpSignalEMA)+")");
//--- get MA handles
   ExtFastMaHandle=iMA(NULL,0,InpFastEMA,0,MODE_EMA,InpAppliedPrice);
   ExtSlowMaHandle=iMA(NULL,0,InpSlowEMA,0,MODE_EMA,InpAppliedPrice);
//--- initialization done
  }
//+------------------------------------------------------------------+
//| Moving Averages Convergence/Divergence                           |
//+------------------------------------------------------------------+
int OnCalculate(const int rates_total,
                const int prev_calculated,
                const datetime &time[],
                const double &open[],
                const double &high[],
                const double &low[],
                const double &close[],
                const long &tick_volume[],
                const long &volume[],
                const int &spread[])
  {
//--- check for data
   if(rates_total<InpSignalEMA)
      return(0);
//--- not all data may be calculated
   int calculated=BarsCalculated(ExtFastMaHandle);
   if(calculated<rates_total)
     {
      Print("Not all data of ExtFastMaHandle is calculated (",calculated,"bars ). Error",GetLastError());
      return(0);
     }
   calculated=BarsCalculated(ExtSlowMaHandle);
   if(calculated<rates_total)
     {
      Print("Not all data of ExtSlowMaHandle is calculated (",calculated,"bars ). Error",GetLastError());
      return(0);
     }
//--- we can copy not all data
   int to_copy;
   if(prev_calculated>rates_total || prev_calculated<0) to_copy=rates_total;
   else
     {
      to_copy=rates_total-prev_calculated;
      if(prev_calculated>0) to_copy++;
     }
//--- get Fast EMA buffer
   if(IsStopped()) return(0); //Checking for stop flag
   if(CopyBuffer(ExtFastMaHandle,0,0,to_copy,ExtFastMaBuffer)<=0)
     {
      Print("Getting fast EMA is failed! Error",GetLastError());
      return(0);
     }
//--- get Slow EMA buffer
   if(IsStopped()) return(0); //Checking for stop flag
   if(CopyBuffer(ExtSlowMaHandle,0,0,to_copy,ExtSlowMaBuffer)<=0)
     {
      Print("Getting slow SMA is failed! Error",GetLastError());
      return(0);
     }
//---
   int limit;
   if(prev_calculated==0)
      limit=0;
   else limit=prev_calculated-1;
//--- calculate MACD
   for(int i=limit;i<rates_total && !IsStopped();i++)
      ExtMacdBuffer[i]=ExtFastMaBuffer[i]-ExtSlowMaBuffer[i];
//--- calculate Signal
   ExponentialMAOnBuffer(rates_total,prev_calculated,0,InpSignalEMA,ExtMacdBuffer,ExtSignalBuffer);
   //--- calculate Histogram
   for(int i=limit;i<rates_total && !IsStopped();i++)
      ExtHistogramBuffer[i]=(ExtMacdBuffer[i]-ExtSignalBuffer[i])*2;
//--- OnCalculate done. Return new prev_calculated.
   return(rates_total);
  }
//+------------------------------------------------------------------+
```

## コンパイル

1. ソースのファイルを MACD2.mq5 の名前で保存します。

2. MT5 のメニューから[ファイル]-[データフォルダを開く]をクリックして、データフォルダを表示します。

3. データフォルダの中の[MQL5]-[Indicators]フォルダに MACD2.mq5 ファイルを保存します。

4. MT5 のメニューから[ツール]-[MetaQuotes 言語エディタ]をクリックして、MetaEditor を表示します。

4. MetaEditor の左側の[Navigator]から[Indicators]-[MACD2.mq5]を右クリックして[Compile]をクリックします。
Navigator が表示されていなければ[View]-[Navigator]をクリックして表示します。

5. Indicators フォルダに MACD2.ex5 ファイルが作成されます。

## インディケーターの追加

1. MT5 のメニューから[挿入]-[インディケータ]-[カスタム]-[MACD2]をクリックします。

2. 表示されたインディケーターのダイアログの[インプット]タブからパラメーターを指定して、OK をクリックします。

パラメーターの内容は次の通りです。

* Fast EMA period: 短期の移動平均を計算する際の期間
* Slow EMA period: 長期の移動平均を計算する際の期間
* Signal EMA period: シグナルラインの移動平均を計算する際の期間
* Applied Price: 短期の移動平均と長期の移動平均を計算する際に適用する価格(始値、高値、安値、終値)

## 終わり

MQL5 は初めて使ってみましたが、C 言語に近いのかな？と感じました。
C 言語もあまり使ったことはないですけど・・

`#property indicator_type1` のようにインディケーターを番号で指定しているところがあまり好きになれないです・・
